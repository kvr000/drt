#!/usr/bin/env perl

###
## drt library
##
## drt multiplatform development toolkit
##
## ----------------------------------------------------------------------------------
##
## Copyright (C) 2004-2008 Zbyněk Vyškovský
##
## ----------------------------------------------------------------------------------
##
## LICENSE:
##
## This file is part of drt
##
## drt is free software; you can redistribute it and/or modify it under the
## terms of the GNU Lesser General Public License as published by the Free
## Software Foundation; either version 3 of the License, or (at your option)
## any later version.
##
## drt is distributed in the hope that it will be useful, but WITHOUT ANY
## WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
## FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for
## more details.
##
## You should have received a copy of the GNU Lesser General Public License
## along with drt; if not, write to the Free Software Foundation, Inc., 51
## Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
##
## @copyright	2004-2008 Zbyněk Vyškovský
## @link	mailto:kvr@matfyz.cz
## @link	http://kvr.matfyz.cz/drt/
## @license	http://www.gnu.org/licenses/lgpl.txt GNU Lesser General Public License v3
###

use strict;
use warnings;

use File::Basename;
use lib dirname($0)."/perl";
use dr::Util;

package KeyValueDbWriter;

use strict;
use warnings;

sub new
{
	my $ref			= shift;
	my $class		= ref($ref) || $ref;
	my $filename		= shift;

	my $this = bless {
		filename		=> $filename,
	}, $class;

	return $this;
}

sub open
{
	my $filename		= shift;

	if ($filename =~ m/\.csvdb$/) {
		return CsvdbKeyValueDbWriter->new($filename);
	}
	else {
		return DbaKeyValueDbWriter->new($filename);
	}
}


package CsvdbKeyValueDbWriter;

use strict;
use warnings;

use base qw(KeyValueDbWriter);

use Text::CSV;

sub new
{
	my $class = shift; $class = ref($class) || $class;

	my $this = $class->SUPER::new(@_);

	$this->{fd} = FileHandle->new($this->{filename}, ">")
		or dr::Util::doDie("failed to open $this->{filename}: $!");
	$this->{csv_manip} = Text::CSV->new({ binary => 1 });

	return $this;
}

sub write
{
	my $this		= shift;
	my $key			= shift;
	my $value		= shift;

	$this->{csv_manip}->combine($key, $value);
	$this->{fd}->print($this->{csv_manip}->string()."\n");
}

sub close
{
	my $this		= shift;
	$this->{fd}->flush()
		or dr::Util::doDie("failed to flush $this->{filename}: $!");
	$this->{fd}->close();
}


package DbaKeyValueDbWriter;

use strict;
use warnings;

use base qw(KeyValueDbWriter);

sub new
{
	my $class = shift; $class = ref($class) || $class;

	my $this = $class->SUPER::new(@_);

	my $db_load = $ENV{"db_load" } || "db4.8_load";

	open($this->{fd}, "| $db_load -t btree -T $this->{filename}")
		or dr::Util::doDie("failed to run $db_load $this->{filename}: $!");

	return $this;
}

sub write
{
	my $this		= shift;
	my $key			= shift;
	my $value		= shift;

	$this->{fd}->print("$key\n$value\n");
}

sub close
{
	my $this		= shift;
	$this->{fd}->close()
		or dr::Util::doDie("failed to flush $this->{filename}: $!");
}


package main;

use strict;
use warnings;

use FileHandle;
use File::Basename;
use Data::Dumper;
use Getopt::Std;
use dr::Util;
#use DB_File;

sub splitCsvLine($)
{
	my $line		= shift;

	$line =~ s/\r?\n$//;
	my @list = ();
	return @list if ($line eq "");

	for (my $p = 0; $p <= length($line); $p++) {
		if (substr($line, $p, 1) eq '"') {
			my $t = "";
			$p++;
			my $c;
			for ($c = $p; ; $c++) {
				if (($c = index($line, '"', $c)) < 0) {
					die "missing ending quote: $line";
				}
				elsif ($c+1 == length($line) || substr($line, $c+1, 1) eq ",") {
					$t .= substr($line, $p, $c-$p);
					$c += 1;
					last;
				}
				elsif (substr($line, $c+1, 1) eq '"') {
					$t .= substr($line, $p, $c+1-$p);
					$c += 1;
					$p = $c+1;
				}
				else {
					die "missing ending quote: $line";
				}
			}
			push(@list, $t);
			$p = $c;
		}
		else {
			my $c = index($line, ',', $p);
			if ($c < 0) {
				$c = length($line);
			}
			push(@list, substr($line, $p, $c-$p));
			$p = $c;
		}
	}
	return @list;
}

sub compareFormats($$)
{
	my ($t0, $t1) = @_;
	my ($c0, $c1) = ($t0, $t1);
	for (;;) {
		my ($f0, $f1);
		($f0, $c0) = ($1, $2) if ($c0 =~ /^.*(%[^sdugf]*)(.*)$/);
		($f1, $c1) = ($1, $2) if ($c1 =~ /^.*(%[^sdugf]*)(.*)$/);
		die "different formats: $t0 $t1" unless ((defined $f0) == (defined $f1));
		last unless (defined $f0 || defined $f1);
		die "different formats: $t0 $t1" if ($f0 ne $f1);
	}
	return 1;
}

die "usage: $0 localize-file input-files..." unless (@ARGV > 1);

our $db_name = shift @ARGV;
our %db_cls;
our $prefix = ($db_name =~ m,^(.*?)([^/]*)$,)[0];
if ($db_name !~ m,^(.*/|)(\w+)/([^/]*)$,) {
	die "db file not in language directory";
}
our $lang = $2;

mkdir("${prefix}/cls");
unlink($_) foreach (glob("${prefix}cls/*.cls.*db"));
unlink($db_name);

our $db_full = KeyValueDbWriter::open($db_name);
my $ext = $db_name;
$ext =~ s/.*\.//g;

my $errors = 0;
eval {
	foreach my $in_file (@ARGV) {
		my $in_csv = FileHandle->new($in_file, "<")
			or die "failed to open $in_file: $!";

		my $col;
		my $count;
		my $line = 0;

		if (defined (my $headline = <$in_csv>)) {
			$line++;
			my @head = splitCsvLine($headline);
			$count = @head;
			for (my $i = 0; ; $i++) {
				if ($i == @head) {
					die "$in_file:1: failed to find language $lang";
				}
				if ($head[$i] eq $lang) {
					$col = $i;
					last;
				}
			}
		}
		else {
			die "$in_file:1: failed to find language header";
			next;
		}

		while (<$in_csv>) {
			$line++;
			my @parsed = splitCsvLine($_);
			next if (@parsed == 0);
			die "$in_file:$line: need $count values: $_: ".Dumper(\@parsed) unless (@parsed == $count);
			next if ($parsed[0] eq "" && $parsed[1] eq "" && $parsed[$col] eq "");
			STDERR->print("$in_file:$line: invalid class name: \'$parsed[0]\'\n"), $errors++, next unless ($parsed[0] =~ m/^(\w|::)+$/);
			$db_full->write("$parsed[0]\t$parsed[1]", $parsed[$col]);
			my $db_class;
			if (!defined ($db_class = $db_cls{$parsed[0]}) && !exists $db_cls{$parsed[0]}) {
				if ($parsed[0] =~ m/^([A-Za-z_]+::)*[A-Za-z_]+$/) {
					my $name = $parsed[0];
					$name =~ s/::/-/g;
					$db_class = KeyValueDbWriter::open("${prefix}cls/$name.cls.$ext");
					$db_cls{$parsed[0]} = $db_class;
				}
			}
			if (defined $db_class) {
				$db_class->write("$parsed[0]\t$parsed[1]", $parsed[$col]);
			}
		}
	}
	$db_full->close();
	foreach (values %db_cls) {
		$_->close();
	}
	!$errors;
}
	or $db_full->close(), unlink($db_name), die $@;
exit(0);
