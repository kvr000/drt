#!/usr/bin/perl

###
## drt library
##
## drt multiplatform development toolkit
##
## ----------------------------------------------------------------------------------
##
## Copyright (C) 2004-2008 Zbyněk Vyškovský
##
## ----------------------------------------------------------------------------------
##
## LICENSE:
##
## This file is part of drt
##
## drt is free software; you can redistribute it and/or modify it under the
## terms of the GNU Lesser General Public License as published by the Free
## Software Foundation; either version 3 of the License, or (at your option)
## any later version.
##
## drt is distributed in the hope that it will be useful, but WITHOUT ANY
## WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
## FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for
## more details.
##
## You should have received a copy of the GNU Lesser General Public License
## along with drt; if not, write to the Free Software Foundation, Inc., 51
## Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
##
## @copyright	2004-2008 Zbyněk Vyškovský
## @link	mailto:kvr@matfyz.cz
## @link	http://kvr.matfyz.cz/drt/
## @license	http://www.gnu.org/licenses/lgpl.txt GNU Lesser General Public License v3
###

use strict;
use warnings;

use FileHandle;
use Data::Dumper;
use Getopt::Std;
use File::Basename;

use lib dirname($0)."/perl";
use dr::FileTransaction;
use dr::FileParser;
use dr::ModelStore;
use dr::Util qw(defvalue tabalign);


our %opts;
getopts('', \%opts);

die "Usage: $0 list_file touch_file root" unless (@ARGV == 3);
my $listname = shift(@ARGV);
my $touch_name = shift(@ARGV);
my $root = shift(@ARGV);
die "passed file does not end with suffix .list" unless ($listname =~ m/(.*)\.list$/);

our $file_trans = dr::FileTransaction->new();

our $model_store = dr::ModelStore->new();

our $list = dr::FileParser->new($listname);


sub readModel($$)
{
	my $location		= shift;
	my $name		= shift;

	my $model = $model_store->loadModel($location, $name);

	return $model;
}

our %FORM_UML_TYPES_PRIM = (
	Boolean			=> "bool",
	Integer			=> "int",
	Long			=> "bigint",
	long			=> "bigint",
	int			=> "int",
	Integer			=> "int",
);

our %FORM_UML_TYPES_VAR = (
	string			=> "string",
	binary			=> "string",
);

sub dieAttrContext($$)
{
	my $field		= shift;
	my $msg			= shift;

	dr::Util::doDie("$field->{owner}->{filename}:0: $field->{owner}->{full}.$field->{name}: $msg");
}

sub translateFormType($)
{
	my $field		= shift;

	eval {
		my ( $type, $tagger ) = $field->getFinalTypeWithTagger();

		if ($type->{stype} eq "primitive") {
			my $ptype = $type->{type};
			if ($ptype =~ m/^(\w+)\((\d+)\)$/) {
				if (my $sqlt = $FORM_UML_TYPES_VAR{$1}) {
					return "$sqlt($2)";
				}
				die "cannot map $ptype";
			}
			else {
				if (my $sqlt = $FORM_UML_TYPES_PRIM{$ptype}) {
					return $sqlt;
				}
				die "cannot map $ptype";
			}
		}
		elsif ($type->{stype} eq "enum") {
			return "int";
		}
		else {
			die "unknown stype $type->{stype} for class $type->{full}";
		}
	}
		or dieAttrContext($field, $@);
}

sub printAttrOptional
{
	my $formfile		= shift;
	my $field		= shift;
	my $name		= shift;

	if (defined (my $v = $field->checkDrTagValue($name))) {
		$formfile->print(tabalign("\t$name", 24).$v."\n");
		return 1;
	}
	return 0;
}

sub printActionOptional
{
	my $formfile		= shift;
	my $action		= shift;
	my $name		= shift;

	if (defined (my $v = $action->checkDrTagValue($name))) {
		$formfile->print(tabalign("\t$name", 24).$v."\n");
		return 1;
	}
	return 0;
}

my $lineno = 0;

my @models = ();

my $errors = 0;

while (defined (my $line = $list->readLine())) {
	$lineno++;
	next if ($line =~ m/^\s*(#.*|)$/);
	chomp($line);
	eval {
		die "invalid format, need \"location classname\"" unless ($line =~ m/^(\S+)\s+((\w+::)*\w+)$/);
		push(@models, readModel($1, $2));
		1;
	}
		or $errors++, STDERR->print($@ =~ m/^\S+:\d+:/ ? $@ : $list->getContext().": $@");
}

foreach my $model (@models) {
	my $file = $model->{full}; $file =~ s/::/\//g;
	my $form = $file_trans->createTruncated("$root/$file.form");
	foreach my $global (qw(req_get req_set)) {
		if (defined (my $value = $model->checkDrTagValue($global))) {
			$form->print(tabalign($global, 16).$value."\n");
		}
	}
	foreach my $field (@{$model->{field_list}}) {
		my ( $type, $tagger ) = $field->getFinalTypeWithTagger();
		eval {
			$form->print(tabalign("field", 16).$field->{name}."\n");
			$form->print(tabalign("\ttext", 24).defvalue($tagger->checkTagValue("text"), $field->{name})."\n");
			$form->print(tabalign("\ttype", 24).translateFormType($field)."\n");
			$form->print(tabalign("\tmandatory", 24).$field->{mandatory}."\n");
			$form->print(tabalign("\tlogical", 24).$tagger->getTagValue("logical")."\n");
			$form->print(tabalign("\tdisplay", 24).$tagger->getTagValue("display")."\n");
			foreach (qw(req_sys req_get req_set req_view req_mode)) {
				printAttrOptional($form, $field, $_);
			}
			1;
		}
			or die "failed to process $model->{full}.$field->{name}: $@: ".Dumper($tagger);
	}
	foreach my $action (@{$model->{action_list}}) {
		eval {
			$form->print(tabalign("action", 16).$action->{name}."\n");
			$form->print(tabalign("\ttext", 24).defvalue($action->checkDrTagValue("text"), $action->{name})."\n");
			$form->print(tabalign("\tclassify", 24).join(",", keys %{$action->{classify}})."\n");
			if (defined (my $form_actioner = $action->checkDrTagValue("form_actioner"))) {
				my $a = $form_actioner eq "_" ? "$model->{full}_$action->{name}" : index($form_actioner, "_") == 0 ? "$model->{full}$form_actioner" : $form_actioner;
				$form->print(tabalign("\tform_actioner", 24).$a."\n");
			}
			foreach (qw(req_sys req_get req_set req_view req_mode)) {
				printActionOptional($form, $action, $_);
			}
			1;
		}
			or STDERR->print("failed to process $model->{full}.$action->{name}: $@"), $errors++;
	}
	$file_trans->closeFile($form);
}

die "Unrecoverable errors occurred" if ($errors);

if ($errors == 0) {
	$file_trans->createTruncated($touch_name);
}
else {
	die "fatal errors occurred";
}

if ($file_trans->commit()) {
}
