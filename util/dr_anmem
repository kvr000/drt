#!/usr/bin/perl

##
## drt library
##
## drt multiplatform development toolkit
##
## ----------------------------------------------------------------------------------
##
## Copyright (C) 2004-2008 Zbyněk Vyškovský
##
## ----------------------------------------------------------------------------------
##
## LICENSE:
##
## This file is part of drt
##
## drt is free software; you can redistribute it and/or modify it under the
## terms of the GNU Lesser General Public License as published by the Free
## Software Foundation; either version 3 of the License, or (at your option)
## any later version.
##
## drt is distributed in the hope that it will be useful, but WITHOUT ANY
## WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
## FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for
## more details.
##
## You should have received a copy of the GNU Lesser General Public License
## along with drt; if not, write to the Free Software Foundation, Inc., 51
## Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
##
## @author	2004-2008 Zbyněk Vyškovský
## @link	mailto:kvr@matfyz.cz
## @link	http://kvr.matfyz.cz/drt/
## @license	http://www.gnu.org/licenses/lgpl.txt GNU Lesser General Public License v3
##

# run the program with DRO_MM=b and analyse its output with this program

use strict;
use warnings;

use Getopt::Std;

our $ok_leaks = "[(<](".join("|", qw(
	_ZN2dr5Const
	_ZN2dr6Static6create
	_ZN2dr11Thread_impl18postinitMainThread
	_ZN2dr5Lockp6s_initEv
	_ZN2dr9Subsystem12getSubsystem
	_ZN2dr6Thread19createMessageSerial
	_ZN2dr26createMemExceptionInstanceEv
)).")";

our %opts;

getopts('sl', \%opts)
	or die "invalid option specified";

my %frequent;
my %mem;

my $order = 0;

my $pending;
my $seq = 0;
my $lineno = 0;

for (;;) {
	if (!defined $pending) {
		last unless (defined ($pending = <>));
		++$lineno;
	}
	if ($pending =~ m/^(allocated|freeing) block 0x([\da-f]+) \((\d+)\)$/) {
		my ( $op, $block, $length, $thread, $backtrace ) = ( $1, $2, $3, undef, undef );
		if (defined ($pending = <>) && ++$lineno && $pending =~ m/^\x00?--- Thread (\w+|\(nil\)) backtrace:$/) {
			$thread = $1;
			$backtrace = "";
			while (defined ($pending = <>)) {
				++$lineno;
				undef $pending, last if ($pending =~ m/^\x00?--- Thread (\w+|\(nil\)) end$/);
				$backtrace .= $pending;
			}
		}
		if ($op eq "allocated") {
			$seq++;
			my $md = $mem{$block};
			if ($md) {
				print("memory $block ($length) allocated for the second time (lineno=$lineno, orig=$md->{lineno}):\n$md->{backtrace}\n");
			}
			$mem{$block} = {
				lineno		=> $lineno,
				block		=> $block,
				length		=> $length,
				thread		=> $thread,
				backtrace	=> $backtrace,
				order		=> $order++,
				seq		=> $seq,
			};
			if ($opts{s} && defined $backtrace && $backtrace !~ m/$ok_leaks/o) {
				my $c = \$frequent{$backtrace};
				$$c = 0 unless (defined $$c);
				$$c++;
			}
		}
		else {
			my $md = $mem{$block};
			if (!$md) {
				print("memory $block ($length) freed but not allocated (line $lineno):\n$backtrace\n");
			}
			else {
				delete $mem{$block};
			}
		}
	}
	else {
		undef $pending;
	}
}

foreach my $md (sort({ $a->{order} <=> $b->{order} } values %mem)) {
	next if ($md->{backtrace} =~ m/$ok_leaks/o);
	print("memory $md->{block} ($md->{length}) seq=$md->{seq} not freed (line $md->{lineno}):\n$md->{backtrace}\n");
}

if ($opts{l}) {
	my %leak_stat;
	foreach my $md (sort({ $a->{order} <=> $b->{order} } values %mem)) {
		next if ($md->{backtrace} =~ m/$ok_leaks/o);
		$leak_stat{$md->{backtrace}} = { count => 0, first => $md->{seq} } unless (defined $leak_stat{$md->{backtrace}});
		$leak_stat{$md->{backtrace}}->{count}++;
	}
	printf("Leak statistics (%d):\n\n", (scalar keys %leak_stat)/2);
	foreach my $b (sort({ $leak_stat{$b}->{count} <=> $leak_stat{$a}->{count} || $leak_stat{$a}->{first} <=> $leak_stat{$b}->{first} } keys %leak_stat)) {
		my $indb = $b;
		$indb =~ s/^/\t/mg;
		print("$leak_stat{$b}->{count} leaks:\n$indb\n");
	}
}

if ($opts{s}) {
	printf("Allocation statistics (%d):\n\n", (scalar keys %frequent)/2);
	foreach my $b (sort({ $frequent{$b} <=> $frequent{$a} } keys %frequent)) {
		my $indb = $b;
		$indb =~ s/^/\t/mg;
		print("$frequent{$b} allocations:\n$indb\n");
	}
}
